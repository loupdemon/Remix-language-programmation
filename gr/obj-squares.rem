Show a grid of everchanging squares.
====================================

make a square of (size) at (grid-pos) :
	half : size ÷ 2
	square : make shape of {
		{-1, -1},
		{ 1, -1},
		{ 1,  1},
		{-1,  1}
	} with size (half)
	x : (size × grid-pos [1]) - half
	y : (size × grid-pos [2]) - half
	square [position] : {x, y}

	create
		shape : square

		draw (me) :
			fill (shape)

		change (my) colour :
			shape [colour] : random colour

base-size : 20
num-cols : std-width ÷ base-size
num-rows : std-height ÷ base-size

squares : an empty list

for (num-rows) rows (down) and (num-cols) columns (across)
	a-square : make a square of (base-size) at {across, down}
	append (a-square) to (squares)

on standard paper
	animate 1 time per sec
		clear layer 1
		no outline
		for each (square) in (squares)
			change (square) colour
			draw (square)
